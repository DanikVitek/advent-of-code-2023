set shell := ["nu.exe", "-c"]

# Use `just work day-01 part1` to work on the specific binary for a specific day's problems
work day part:
    cargo watch -x "check -p {{day}}" -s "just test {{day}} {{part}}" -s "just lint {{day}}" -s "just bench {{day}} {{part}}" -s "just flamegraph {{day}} {{part}}"
run day part:
    cargo run -p {{day}} --bin {{part}} -r
run-dev day part:
    cargo run -p {{day}} --bin {{part}}
www-watch:
    RUST_LOG=info cargo +nightly leptos watch --project www
www-build:
    cargo +nightly leptos build --project www --release
lint day:
    cargo clippy -p {{day}}
test day part:
    cargo nextest run -p {{day}} {{part}}
bench-all:
    cargo bench -q | save benchmarks/benchmarks.txt
bench day part:
    cargo bench --bench {{day}} {{part}} | save --append benchmarks/{{day}}.bench.txt
flamegraph day part:
    cargo flamegraph --profile flamegraph --root --package {{day}} --bin {{part}} -o flamegraphs/{{day}}_{{part}}.svg
dhat day part:
    cargo run --profile dhat --features dhat-heap --package {{day}} --bin {{part}}
create day:
    cargo generate --path ./daily-template --name {{day}}